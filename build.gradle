import java.nio.file.Files

plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+'
}

version = 'A1.0'
group = 'com.limachi.dim_bag'
archivesBaseName = 'dim_bag'
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

configurations.all {
    // Check for updates every build, usefull because i don't always bump the version of my lib while testing
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: 'official', version: '1.19.2'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg') // Currently, this location cannot be changed from the default.

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'dim_bag'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

            mods {
                dim_bag {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'dim_bag'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

            mods {
                dim_bag {
                    source sourceSets.main
                }
            }
        }

        gameTestServer {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'dim_bag'

            mods {
                dim_bag {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'dim_bag', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                dim_bag {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url = "https://maven.theillusivec4.top/"
    }

    maven { // TOP
        url "https://maven.k-4u.nl"
    }

    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.dev"
    }

    maven { url "https://maven.bai.lol" } //WAILA

    maven { url 'https://www.cursemaven.com' } //General Curse Maven

    maven {
        name "bytesafe"
        url "https://limachi.bytesafe.dev/maven/limlib/"
        credentials {
            username "bytesafe"
            password = Files.readAllLines(file("../bytesafe").toPath())[0]
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.19.2-43.1.25'

    dependencies {
        runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:1.19.2-5.1.1.0")
        compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:1.19.2-5.1.1.0:api")

        // compile against the JEI API but do not include it at runtime
        compileOnly(fg.deobf("mezz.jei:jei-${mc_version}-common-api:${jei_version}"))
        compileOnly(fg.deobf("mezz.jei:jei-${mc_version}-forge-api:${jei_version}"))
        // at runtime, use the full JEI jar for Forge
        runtimeOnly(fg.deobf("mezz.jei:jei-${mc_version}-forge:${jei_version}"))

        implementation(fg.deobf("curse.maven:the-one-probe-245211:${top_file}"))

        implementation fg.deobf("com.limachi.lim_lib:lim_lib:1.19.2-A1.2.0") { changing = true }
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "dim_bag",
                "Specification-Vendor"    : "limachi",
                "Specification-Version"   : project.jar.archiveVersion,
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "limachi",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            credentials {
                username "bytesafe"
                password = Files.readAllLines(file("../bytesafe").toPath())[0]
            }
            name "bytesafe"
            url "https://limachi.bytesafe.dev/maven/dimbag/"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
